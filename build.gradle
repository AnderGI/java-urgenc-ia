
plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.andergi'
version = '0.0.1-SNAPSHOT'
description = 'java-urgenc-ia'

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        // implementation type dependencies are the ones used by the application
        implementation 'org.springframework.boot:spring-boot-starter-webmvc'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
        runtimeOnly 'org.postgresql:postgresql'
        testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('view_paths') {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }
}


subprojects {
    group = "com.andergi.${rootProject.name}"

    sourceSets {
        main {
            java { srcDirs = ['main'] }
            resources { srcDirs = ['main/resources'] }
        }

        test {
            java { srcDirs = ['test'] }
            resources { srcDirs = ['test/resources'] }
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}



sourceSets {
    main {
        java { srcDirs = ['apps/main'] }
        resources { srcDirs = ['apps/main/resources'] }
    }

    test {
        java { srcDirs = ['apps/test'] }
        resources { srcDirs = ['apps/test/resources'] }
    }
}

bootJar {
    archiveBaseName.set('java-urgenc-ia')
    archiveVersion.set('0.0.1')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    implementation project(":backoffice")
}